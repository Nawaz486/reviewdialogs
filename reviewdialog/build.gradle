plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.jetbrains.kotlin.android)
    id("maven-publish")
}

android {
    namespace 'com.review.reviewdialog'
    compileSdk 34

    defaultConfig {
        minSdk 24
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    lint {
        baseline = file("lint-baseline.xml")
    }
}

// Define properties using `ext`
ext {
    PUBLISH_GROUP_ID = "com.github.Nawaz486"
    PUBLISH_VERSION = "1.0-alpha"
    PUBLISH_ARTIFACT_ID = "reviewdialog"
    PUBLISH_DESCRIPTION = "An android gradle plugin for review dialogs"
    PUBLISH_URL = "https://github.com/Nawaz486/reviewdialogs"
    PUBLISH_LICENSE_NAME = "Apache 2.0 License"
    PUBLISH_LICENSE_URL = "https://www.apache.org/licenses/LICENSE-2.0"
    PUBLISH_DEVELOPER_ID = "Muhammad Nawaz"
    PUBLISH_DEVELOPER_NAME = "Muhammad Nawaz"
    PUBLISH_DEVELOPER_EMAIL = "mailto:muhammadnawaz.mn980@gmail.com"
    PUBLISH_SCM_CONNECTION = "scm:git:github.com/Nawaz486/reviewdialogs.git"
    PUBLISH_SCM_DEVELOPER_CONNECTION = "scm:git:ssh://github.com/Nawaz486/reviewdialogs.git"
    PUBLISH_SCM_URL = "https://github.com/Nawaz486/reviewdialogs.git"
}

// Set version and group using `ext` properties
version = project.ext.PUBLISH_VERSION
group = project.ext.PUBLISH_GROUP_ID

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            create("release", MavenPublication) {
                // Applies the component for the release build variant.
                from components["release"]

                // Use the properties from the `ext` block
                groupId = project.ext.PUBLISH_GROUP_ID
                artifactId = project.ext.PUBLISH_ARTIFACT_ID
                version = project.ext.PUBLISH_VERSION

                // Add additional metadata to the POM
                pom {
                    name.set(project.ext.PUBLISH_ARTIFACT_ID)
                    description.set(project.ext.PUBLISH_DESCRIPTION)
                    url.set(project.ext.PUBLISH_URL)

                    licenses {
                        license {
                            name.set(project.ext.PUBLISH_LICENSE_NAME)
                            url.set(project.ext.PUBLISH_LICENSE_URL)
                        }
                    }

                    developers {
                        developer {
                            id.set(project.ext.PUBLISH_DEVELOPER_ID)
                            name.set(project.ext.PUBLISH_DEVELOPER_NAME)
                            email.set(project.ext.PUBLISH_DEVELOPER_EMAIL)
                        }
                    }

                    scm {
                        connection.set(project.ext.PUBLISH_SCM_CONNECTION)
                        developerConnection.set(project.ext.PUBLISH_SCM_DEVELOPER_CONNECTION)
                        url.set(project.ext.PUBLISH_SCM_URL)
                    }
                }
            }
        }
    }
}

dependencies {
    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core

    //sdp
    implementation libs.sdp.android
    //InApp Review
    implementation libs.review.ktx
    //Lottie Animation
    implementation libs.lottie
}
